// The first process is an EPR source, which constructs an EPR pair
// and sends the qubits on channels EPRtoA and EPRtoB.
// Alice will receive on EPRtoA, Bob will receive on EPRtoB.

// EPR source
newqubit y ;         // A fresh qubit in state |0> 
newqubit z ;         // Another fresh qubit in state |0>
H(y) ;               // Apply Hadamard to put y into state |0> + |1>
CNOT(y,z) ;          // Apply Controlled Not, to entangle y and z: state |00> + |11>
send y on EPRtoA ;   // Send qubit y on channel c
send z on EPRtoB ;   // Send qubit z on channel d
nil                  // Stop

|

// The next process is Alice, who inputs an unknown qubit and 
// teleports it to Bob.
// Alice receives her part of the EPR pair on channel EPRtoA.
// Alice uses channel AtoB for classical communication, sending measurement
// results to Bob.

// Alice

(
input x ;                // This is the unknown qubit input
receive y on EPRtoA ;    // Receive part of the EPR pair on channel EPRtoA
CNOT(x,y) ;              // These four steps are the 
H(x) ;                   // teleportation protocol,
m := measure x ;         // applying quantum operations 
n := measure y ;         // and measuring in the standard basis.
send m on AtoB ;         // Send first measurement result to Bob on channel AtoB
send n on AtoB ;         // Send second measurement result to Bob on channel AtoB
nil                      // Stop

|

// The next process is Bob, who receives classical data from Alice
// and uses it to transform his part of the EPR pair, in order to
// recover the original unknown qubit that Alice had.

// Bob

receive z on EPRtoB ;    // Receive part of the EPR pair on channel EPRtoB
receive m on AtoB ;      // Receive first classical bit from Alice on channel AtoB
receive n on AtoB ;      // Receive second classical bit from Alice on channel AtoB
if n then X(z) ;         // These two steps are the conditional transformations of
if m then Z(z) ;         // Bob's part of the EPR pair, based on the classical data.
output z ;               // Announce the recovered qubit
nil                      // Stop
)




